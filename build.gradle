plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'org.ajoberstar.grgit' version '4.1.1'
    id "org.sonarqube" version "3.3"

}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xdoclint:none', '-Xlint:none', '-nowarn']
}

sonarqube {
    properties {
        property "sonar.projectKey", "gaelkalume_megamek"
        property "sonar.login", "6b02965550bc0e2bccb76902aab953f1af062d8a"
        property "sonar.organization", "gaelkalume"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src"
        property "sonar.branch", "projet"
    }
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' } // SerialKiller master branch
    }
}

subprojects {
    group = 'org.megamek'
    version = '0.49.8-SNAPSHOT'
}

ext {
    mmGitRoot = 'https://github.com/MegaMek/megamek.git'
    // Work on MML or MHQ sometimes requires changes in MM as well. The maven publishing tasks use
    // these properties to append the branch name to the artifact id if the repo is not in the master
    // branch, making it available separately to the child project. 
    mmBranch = grgit.branch.current().name
    mmBranchTag = mmBranch.equals('projet') ? '' : '-' + mmBranch
}

// A properties_local.gradle file can be used to override any of the above options. For instance,
// rootProject.ext.mmGitRoot = 'file:///path/to/local/repo' will cause the release target to clone a
// local copy of the repository rather than downloading it.

def localProperties = file('properties_local.gradle')
if (localProperties.exists()) {
    apply from: localProperties
}
